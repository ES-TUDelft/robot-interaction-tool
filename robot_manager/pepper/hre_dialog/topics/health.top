topic: ~health()
language: enu

include: general.top

u: (e:startTopicTag) ^gotoReactivate(startTopicTag)
u: (e:endTopicTag) "Ok, time is up. Back to our discussion." $blockCompleted=1 ^gotoReactivate(resetWebPageTag)

u: ({can you} repeat {please}) "Sure, i'll repeat." ^gotoReactivate($lastTagName)

# PROPOSALS:
# ----------
proposal: %startTopicTag $lastTagName=startTopicTag $pageHeading="Secrets for a healthy life style?" $pageText="?" ^gotoReactivate(infoPageTag) ^gotoReactivate(healthyTag)

proposal: %infoPageTag $pageName="displayinfo" ^topicTagReactivate(general, getAndFillPageTag)

proposal: %healthyTag $anim=["^notExist(customAnimation) ~neutralAnimation" "$customAnimation"] "^start($anim) Do you know the secrets for a healthy lifestyle? ^wait($anim)"
    u1: (~yes) "Oh waw! You are a savant." ^gotoReactivate(howAreYouTag)
    u1: (~no) "Ok, listen: " $pageText="1- Sleep well. 2- Avoid Junk food. 3- Build good relationships with funny robots." ^gotoReactivate(infoPageTag) ^gotoReactivate(secretsTag)

proposal: %secretsTag "First, you have to sleep well." "Second, avoid junk food at all cost." "Third, build good relationships with funny robots like me." ^gotoReactivate(resetWebPageTag) $wait=2000 ^gotoReactivate(howAreYouTag)

# Asking: how are you
proposal: %howAreYouTag $lastTagName=howAreYouTag "Tell me, How are you feeling today?"
    u1: ({~i_am} {feeling} {i feel} {all is} _["{rather} fine" "{very} good" great ok super perfect "{rather} well" awesome] {"thank you"} {thanks} ) "Good to know ["^notExist(iAmFine) ~i_am feeling $1 as well" "^empty $iAmFine==1"]. " ^gotoReactivate(painLevelTag)
    u1: ([~i_dont_feel_good ~im_sick ~im_tired bad "{~i_am} not {feeling} good" horrible] ) "Oh, sorry to hear that." ^gotoReactivate(painLevelTag)

proposal: %painLevelTag $lastTagName=painLevelTag "Can you rate your pain level, from 1 to 10, with 10 being in too much pain?"
    u1: ([1 2 3 4 5]) "Ok, no worries." ^gotoReactivate(continueTag)
    u1: ([6 7 8]) "Do not worry, we will take care of you as soon as possible." ^gotoReactivate(continueTag)
    u1: ([9 10]) "Oh no! I will notify the doctor now." ^gotoReactivate(continueTag)

proposal: %continueTag $lastTagName=continueTag "Alright, back to our task." $blockCompleted=1 ^gotoReactivate(resetWebPageTag)

proposal: %resetWebPageTag $lastTagName=resetWebPageTag $pageName="index" ^topicTagReactivate(general, loadPageTag)